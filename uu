//@version=5
strategy("555 with BB Filter & Visualization", overlay=true, default_qty_type=strategy.cash, initial_capital=100, currency=currency.USD)

// 1. 指标计算 =========================================
// EMA指标
ema6 = ta.ema(close, 20)
ema21 = ta.ema(close, 60)
ema55 = ta.ema(close, 240)
ema600 = ta.ema(close, 600)

// 布林带指标
length = 8
src = close
mult = 1.5
basis = ta.sma(src, length)
dev = mult * ta.stdev(src, length)
upperBB = basis + dev
lowerBB = basis - dev

// 2. 动态通道宽度计算
bandWidth = math.abs(ema6 - ema21)
minWidth = ta.sma(bandWidth, 150)
widthRatio = bandWidth / minWidth
validTrade = widthRatio > 0.01 and widthRatio < 0.1

// 3. 布林带带宽过滤低波动
bbWidth = (upperBB - lowerBB) / basis
lowVolatility = bbWidth < 0.01
validTrade := validTrade and not lowVolatility

// 4. 交易信号 =====================================
emaLong = ta.crossover(ema6, ema21) and close > ema55 and validTrade
emaShort = ta.crossunder(ema6, ema21) and close < ema55 and validTrade

enterLong = emaLong 
enterShort = emaShort

// 5. 止损计算 =====================================
longStopPrice = ta.lowest(low, 50)
shortStopPrice = ta.highest(high, 50)

// 6. 风险参数 =====================================
leverage = 20
riskPercent = 25
takeProfitPercent = 5.5

// 7. 动态仓位计算
equity = strategy.equity
riskCapital = equity * riskPercent / 100
positionSize = (riskCapital * leverage) / close

// 8. 止盈计算
longTakeProfit = close * (1 + takeProfitPercent/100)
shortTakeProfit = close * (1 - takeProfitPercent/100)

// 9. 延迟执行逻辑
var bool delayedEnterLong = false
var bool delayedEnterShort = false

if enterLong
    delayedEnterLong := true
else if barstate.isconfirmed and delayedEnterLong
    strategy.entry("Long", strategy.long, qty=positionSize)
    strategy.exit("Exit Long", "Long", stop=longStopPrice, limit=longTakeProfit)
    delayedEnterLong := false

if enterShort
    delayedEnterShort := true
else if barstate.isconfirmed and delayedEnterShort
    strategy.entry("Short", strategy.short, qty=positionSize)
    strategy.exit("Exit Short", "Short", stop=shortStopPrice, limit=shortTakeProfit)
    delayedEnterShort := false

// 10. 退出条件
exitLong = ta.crossunder(ema6, ema21) 
exitShort = ta.crossover(ema6, ema21) 

if exitLong
    strategy.close("Long")
if exitShort
    strategy.close("Short")

// 11. 可视化增强 ======================================
// 绘制EMA线
// plot(ema6, color=color.blue, title="EMA 6")
// plot(ema21, color=color.red, title="EMA 21")
// plot(ema55, color=color.green, title="EMA 55")

// 绘制布林带
// plot(upperBB, color=color.purple, title="Upper BB")
// plot(lowerBB, color=color.purple, title="Lower BB")



// 绘制交易信号
plotshape(enterLong, title="Buy Signal", style=shape.triangleup, location=location.belowbar, color=color.green, size=size.small)
plotshape(enterShort, title="Sell Signal", style=shape.triangledown, location=location.abovebar, color=color.red, size=size.small)